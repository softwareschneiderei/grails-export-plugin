buildscript {
    repositories {
        maven { url "https://repo.grails.org/grails/core" }
        mavenCentral()
    }
    dependencies { // Not Published to Gradle Plugin Portal
        classpath "org.grails:grails-gradle-plugin:6.2.4"
    }
}

plugins {
    id "groovy"
    id "java-library"
    id "war"
    id "idea"
    id "application"
    id "eclipse"
    id "maven-publish"
}

// Not Published to Gradle Plugin Portal
apply plugin: "org.grails.grails-plugin"

group "org.grails.plugins"

repositories {
    mavenCentral()
    maven {
        name "Grails"
        url "https://repo.grails.org/grails/core"
    }
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-logging'
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-autoconfigure"
    implementation "org.springframework.boot:spring-boot-starter-tomcat"

    implementation "org.grails:grails-web-boot"
    implementation "org.grails:grails-dependencies"

    api 'net.sf.opencsv:opencsv:2.3'

    api 'com.lowagie:itext:2.1.7'
    api "com.lowagie:itext-rtf:2.1.7"
//    api 'com.github.librepdf:openpdf:1.4.2'
//    api 'com.github.librepdf:openrtf:1.2.1'
    runtimeOnly 'xerces:xercesImpl:2.12.2'

    api 'org.odftoolkit:simple-odf:0.6.6'
    api 'net.sourceforge.jexcelapi:jxl:2.6.12'
    api 'commons-beanutils:commons-beanutils:1.9.4'
    api 'commons-codec:commons-codec:1.17.0'
}

java {
    sourceCompatibility = JavaVersion.toVersion("11")
    targetCompatibility = JavaVersion.toVersion("11")
}

jar {
    exclude 'gsp**'

    List excludes = ['UrlMappings', 'TestController']
    excludes.each {
        exclude "**/${it}*.class"
    }
    eachFile { copyDetails ->
        if (copyDetails.path.contains('grails-plugin.xml')) {
            filter { line -> excludes.find{line.contains(it)}? null : line}
        }
    }
    archiveClassifier=''
}

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                name = "Grails Export Plugin"
                description = "This plugin offers export functionality supporting different formats e.g. CSV, Excel, Open Document Spreadsheet, PDF and XML and can be extended to add additional formats."

                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "graemerocher"
                        name = 'Graeme Rocher'
                    }
                    developer {
                        id = "puneetbehl"
                        name = 'Puneet Behl'
                    }
                    developer {
                        id = "nwwells"
                        name = 'Nathan Wells'
                    }
                    developer {
                        id = "tulu"
                        name = 'Ruben'
                    }
                    developer {
                        id = "arturoojeda"
                        name = 'Arturo Ojeda López'
                    }
                    developer {
                        id = "fabiooshiro"
                        name = 'Fabio Issamu Oshiro'
                    }
                    developer {
                        id = "ddelponte"
                        name = 'Dean Del Ponte'
                    }
                    developer {
                        id = "cristallo"
                        name = 'Cristiano Limiti'
                    }
                    developer {
                        id = "mirweb"
                        name = 'Mirko Weber'
                    }
                    developer {
                        id = "joasgarcia"
                        name = 'Joás Garcia'
                    }
                    developer {
                        id = "frangarcia"
                        name = 'Fran García'
                    }
                    developer {
                        id = "dustindclark"
                        name = 'Dustin Clark'
                    }
                    developer {
                        id = "miq"
                        name = 'Mihael Koep'
                        email = 'mihael.koep@softwareschneiderei.de'
                    }
                }
            }

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            from components.java
            repositories {
                maven {
                    credentials {
                        username "$mavenUser"
                        password "$mavenPassword"
                    }
                    url "$mavenUrl"
                }
            }
        }
    }
}
